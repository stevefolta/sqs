#!/usr/bin/env sqs

import ParseNode
import Context
import Codegen
import Parser
import Builtins
import Calls
import Debugging


export class ImportStatement: ParseNode
	(names block)

	init(names-in, block-in)
		names = names-in
		block = block-in

	resolve-names(builder)
		for name: names
			module = get-module(name)
			module.resolve-names(builder)
			block.add-imported-module(module)

	emit-top-level-declarations(builder)
		for name: names
			module = get-module(name)
			if !module.declarations-emitted
				module.emit-top-level-declarations(builder)
	emit-top-level(builder)
		for name: names
			module = get-module(name)
			if !module.is-emitted
				module.emit-top-level(builder)

	emit(builder)
		# Make sure the modules have been initialized.
		for name: names
			module = get-module(name)
			builder.emit-module-initialization(module)

export class ExportStatement: ParseNode
	(item)

	init(item-in)
		item = item-in

	resolve-names(builder)
		item.resolve-names(builder)
	emit-top-level-declarations(builder)
		item.emit-top-level-declarations(builder)
	emit-top-level(builder)
		item.emit-top-level(builder)
	emit(builder)
		# Only SetExprs will do something here.
		item.emit(builder)


export class Module
	(name block exported-classes exported-functions exported-vars)
	(names-are-resolved declarations-emitted is-emitted)

	init(name-in)
		name = name-in
		exported-classes = {}
		exported-functions = {}
		exported-vars = {}
	block=(new-block)
		block = new-block
	reset-emission
		declarations-emitted = is-emitted = false

	resolve-names(builder)
		if names-are-resolved
			return
		names-are-resolved = true

		# Start again with a new GlobalContext.
		builder.push-context(GlobalContext())

		block.resolve-names(builder)

		builder.pop-context	# the GlobalContext.

	emit-top-level-declarations(builder)
		declarations-emitted = true
		block.emit-top-level-declarations(builder)

	emit-top-level(builder)
		# Mark ourself as emitted before emitting the block, so its imports can't
		# cause an infinite loop.
		is-emitted = true
		builder.emit-module(self)

	context-for(builder)
		# Called by Block.resolve-names() to get a context.
		return ModuleContext(self)

	get-export(name)
		class-statement = exported-classes[name]
		if class-statement
			return ClassRef(class-statement)
		function = exported-functions[name]
		if function
			# Treat it as a nullary function call.  FunctionCallExpr.emit will
			# append arguments if any are given.
			return FunctionCallExpr(FunctionRef(function), [])
		var = exported-vars[name]
		if var
			return ModuleLocal(self, name)
		return nil

	variable-index(name)
		index = 0
		for kv: block.locals
			if kv.key == name
				return index
			index += 1
		return -1

	class ModuleContext: Context
		(module)

		init(module-in)
			module = module-in

		find(name)
			local = module.block.get-local(name)
			if local
				return ModuleLocal(module, name)
			expr = module.block.find(name)
			if expr
				return expr
			return parent && parent.find(name)
		find-autodeclaring(name)
			# Already declared in the block?
			local = module.block.get-local(name)
			if local
				return ModuleLocal(module, name)
			# Not declared here, search up the chain.
			expr = parent && parent.find(name)
			if expr
				return expr
			# Doesn't exist, so autodeclare it.
			local = module.block.autodeclare(name)
			return ModuleLocal(module, name)

		generated-prefix(builder)
			# Modules are the top level; ignore the parent.
			return builder.mangle-name(module.name)

	class ModuleLocal: ParseNode
		(name module)

		init(module-in, name-in)
			name = name-in
			module = module-in
		is-settable
			return true
		emit(builder)
			return builder.emit-module-local-get(module, name)
		emit-set(value, builder)
			value-loc = value.emit(builder)
			return builder.emit-module-local-set(module, name, value-loc)


modules = {}

export fn get-module(name)
	if modules.contains(name)
		return modules[name]
	module = Module(name)
	modules[name] = module
	parser = Parser(File("{Path(argv[1]).dir-name}/{name}"), name)
	module.block = parser.parse-block(module)
	return module

export fn reset-module-emission()
	for kv: modules
		kv.value.reset-emission()

